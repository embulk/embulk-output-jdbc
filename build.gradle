
plugins {
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.jruby-gradle.base' version '1.6.0'
    id 'java'
    id 'checkstyle'
}
import com.github.jrubygradle.JRubyExec

def bintrayReleaseProjects = [
    project(":embulk-output-jdbc"),
]

allprojects {
    group = 'org.embulk.output.jdbc'
    version = '0.8.7'

    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    bintray {
        // write at your bintray user name and api key to ~/.gradle/gradle.properties file:
        // bintray_user=frsyuki
        // bintray_api_key=xxxxxxxxxxx
        user = project.hasProperty('bintray_user') ? bintray_user : ''
        key = project.hasProperty('bintray_api_key') ? bintray_api_key : ''

        publications = ['bintrayMavenRelease']

        dryRun = !bintrayReleaseProjects.contains(project)
        publish = true

        pkg {
            userOrg = 'embulk-output-jdbc'
            repo = 'maven'
            name = project.name
            desc = 'MySQL, PostgreSQL, Redshift and generic JDBC output plugins for Embulk'
            websiteUrl = 'https://github.com/embulk/embulk-output-jdbc'
            issueTrackerUrl = 'https://github.com/embulk/embulk-output-jdbc/issues'
            vcsUrl = 'https://github.com/embulk/embulk-output-jdbc.git'
            licenses = ['Apache-2.0']
            labels = ['embulk', 'java']
            publicDownloadNumbers = true

            version {
                name = project.version
            }
        }
    }
}

subprojects {
    apply plugin: 'maven'  // install jar files to the local repo: $ gradle install
    apply plugin: 'java'
    apply plugin: 'findbugs'
    //apply plugin: 'jacoco'
    apply plugin: 'com.github.jruby-gradle.base'

    repositories {
        mavenCentral()
        jcenter()

        maven {
            url "https://dl.bintray.com/embulk-input-s3/maven"
        }
    }

    configurations {
        provided
        defaultJdbcDriver
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile  'org.embulk:embulk-core:0.9.20'
        provided 'org.embulk:embulk-core:0.9.20'
        testCompile 'org.embulk:embulk-test:0.9.20'
        testCompile 'org.embulk:embulk-standards:0.9.20'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" //<< "-Xlint:deprecation"
        options.encoding = 'UTF-8'
    }
    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    findbugs {
        ignoreFailures = true
    }

    javadoc {
        options {
            locale = 'en_US'
            encoding = 'UTF-8'
        }
    }

    task classpath(type: Copy, dependsOn: ["jar"]) {
        doFirst { file('classpath').deleteDir() }
        from (configurations.runtime - configurations.provided - configurations.defaultJdbcDriver + files(jar.archivePath))
        into 'classpath'
    }
    task defaultJdbcDriver(type: Copy, dependsOn: ["jar"]) {
        doFirst { file('default_jdbc_driver').deleteDir() }
        from (configurations.defaultJdbcDriver)
        into 'default_jdbc_driver'
    }
    clean {
        delete 'classpath'
        delete 'default_jdbc_driver'
    }

    checkstyle {
        configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
        toolVersion = '6.14.1'
    }
    checkstyleMain {
        configFile = file("${project.rootDir}/config/checkstyle/default.xml")
        ignoreFailures = true
    }
    checkstyleTest {
        configFile = file("${project.rootDir}/config/checkstyle/default.xml")
        ignoreFailures = true
    }
    task checkstyle(type: Checkstyle) {
        classpath = sourceSets.main.output + sourceSets.test.output
        source = sourceSets.main.allJava + sourceSets.test.allJava
    }

    task gem(type: JRubyExec, dependsOn: ['build', 'gemspec', 'classpath', 'defaultJdbcDriver']) {
        jrubyArgs '-rrubygems/gem_runner', "-eGem::GemRunner.new.run(ARGV)", 'build'
        scriptArgs "${project.projectDir.absolutePath}/build/gemspec"
        doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "${parent.projectDir}/pkg") }
    }

    test {
        testLogging {
            outputs.upToDateWhen {false}
            showStandardStreams = true
        }
    }

    // add tests/javadoc/source jar tasks as artifacts to be released
    task testsJar(type: Jar, dependsOn: classes) {
        classifier = 'tests'
        from sourceSets.test.output
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives testsJar, sourcesJar, javadocJar
    }

    publishing {
        publications {
            bintrayMavenRelease(MavenPublication) {
                from components.java
                artifact testsJar
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    task gemspec {
        doLast {
            file('build').mkdirs();
            file('build/gemspec').write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Sadayuki Furuhashi"]
  spec.summary       = %[JDBC output plugin for Embulk]
  spec.description   = %[Inserts or updates records to a table.]
  spec.email         = ["frsyuki@gmail.com"]
  spec.licenses      = ["Apache 2.0"]
  spec.homepage      = "https://github.com/embulk/embulk-output-jdbc"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"] + Dir["default_jdbc_driver/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]
end
/$)
        }
    }
}

task gempush {
    doLast {
        "gem push pkg/embulk-output-jdbc-${project.version}.gem".execute().waitFor()
        "gem push pkg/embulk-output-mysql-${project.version}.gem".execute().waitFor()
        "gem push pkg/embulk-output-postgresql-${project.version}.gem".execute().waitFor()
        "gem push pkg/embulk-output-redshift-${project.version}.gem".execute().waitFor()
        "gem push pkg/embulk-output-oracle-${project.version}.gem".execute().waitFor()
        "gem push pkg/embulk-output-sqlserver-${project.version}.gem".execute().waitFor()
        "gem push pkg/embulk-output-db2-${project.version}.gem".execute().waitFor()
    }
}
